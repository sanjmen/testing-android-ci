buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.cuerty.androidtraviscitest"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            def cmd = "git log -1 HEAD --pretty=format:%s"
            def proc = cmd.execute()
            def commit_message = proc.text.trim()
            ext.betaDistributionReleaseNotes = commit_message
        }
    }

    applicationVariants.all { variant ->
        def is_travis = Boolean.valueOf(System.getenv("TRAVIS"));
        if (is_travis) {
            variant.mergedFlavor.versionCode = System.getenv("TRAVIS_BUILD_NUMBER") as Integer ?: 1;
            variant.mergedFlavor.versionName = String.format("1.0b%d", variant.mergedFlavor.versionCode);
        }
        project.ext.set("archivesBaseName", "AndroidTravisCITest-" + versionName + "-" + versionCode);

    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
}
